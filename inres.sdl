From aztec!cs.uct.ac.za!heinz Wed Jul 20 20:47:05 1994
Return-Path: <aztec!cs.uct.ac.za!heinz>
Received: by gramix.aztec.co.za (/\==/\ Smail3.1.25.1 #25.30)
	id <m0qQiXg-000GLWC@gramix.aztec.co.za>; Wed, 20 Jul 94 20:47 GMT
Received: from cs.uct.ac.za by aztec.co.za with smtp
	(Smail3.1.28.1 #1) id m0qQZqm-000KdhC; Wed, 20 Jul 94 13:30 EET
Received: from ariel.cs.uct.ac.za by cs.uct.ac.za with smtp
	(Smail3.1.28.1 #2) id m0qQZrP-0009EDC; Wed, 20 Jul 94 13:30 SAT (+0200)
Message-Id: <m0qQZrP-0009EDC@cs.uct.ac.za>
From: heinz@cs.uct.ac.za (Heinz Kabutz)
Subject: inresmain.sdl
To: gram@cs.uct.ac.za (Graham Wheeler)
Date: Wed, 20 Jul 1994 13:30:39 +0200 (SAT)
X-Mailer: ELM [version 2.4 PL23]
MIME-Version: 1.0
Content-Type: text/plain; charset=US-ASCII
Content-Transfer-Encoding: 7bit
Content-Length: 3422      
Status: RO

SYSTEM Inres_Protocol

/* Left out medium - Initiator and Responder exchange signals directly. */

  NEWTYPE ISDUtype
          STRUCT start Time ;
  ENDNEWTYPE ISDUtype

  NEWTYPE IPDUtype
          LITERALS cr, cc, dr, dt, ak ;
  ENDNEWTYPE IPDUtype

  NEWTYPE MSDUtype
          STRUCT id IPDUtype ;
          Num Natural ;
          Data ISDUtype ;
  ENDNEWTYPE MSDUtype

  SIGNAL ICONreq,
         IDATreq (IDSUtype),
         ICONconf,
         ICONind,
         ICONresp,
         IDISreq,
         IDISind,
         IDATind (IDSUtype),
         CR,
         CC,
         DT (Natural, ISDUtype),
         AK (Natural),
         DR;

  CHANNEL ISAP1ini FROM ENV TO Ini_Station
          WITH ICONreq, IDATreq ;
  ENDCHANNEL ISAP1ini ;

  CHANNEL ISAP2ini FROM Ini_Station TO ENV
          WITH ICONconf, IDISind ;
  ENDCHANNEL ISAP2ini ;

  CHANNEL ISAP1rec FROM ENV TO Resp_Station
          WITH ICONresp, IDISresp ;
  ENDCHANNEL ISAP1rec ;

  CHANNEL ISAP2rec FROM Resp_Station TO ENV
          WITH ICONind, IDATind ;
  ENDCHANNEL ISAP2rec ;

  CHANNEL MSAP1resp FROM Resp_Station TO Ini_Station
          WITH DR, CC, AK ;
  ENDCHANNEL MSAP1resp ;

  CHANNEL MSAP1ini FROM Ini_Station TO Resp_Station
          WITH CR, DT ;
  ENDCHANNEL MSAP1ini ;

  BLOCK ENV ;

    PROCESS User_Initiator (1,1) ;
      SIGNALSET ICONconf, IDISind ;

      DCL Pd Duration := 10.0,
          Pc Duration := 12.0,
          d  ISDUtype ;
    
      TIMER tc, td ;

        START ;
            Connection_Establishment:
              OUTPUT ICONreq ;
              NEXTSTATE Confirmation ;

        STATE Confirmation ;
          INPUT ICONconf ;
            Data_Phase:
              TASK d!start := NOW ;
              OUTPUT IDATreq (d) ;
              SET (NOW+Pd, td) ;
              NEXTSTATE Connect ;
          INPUT IDISind ;
              SET (NOW+Pc, tc) ;
              NEXTSTATE Wait ;
        ENDSTATE Confirmation ;

        STATE Wait ;
          INPUT tc ;
              JOIN Connection_Establishment ;
        ENDSTATE Wait ;

        STATE Connect
          INPUT IDISind ;
              RESET (td) ;
              JOIN Connection_Establishment ;
          INPUT td ;
              JOIN Data_Phase ;
        ENDSTATE Connect ;

    ENDPROCESS User_Initiator ;


    PROCESS User_Responder (1,1) ;
      SIGNALSET ICONind, IDATind ;

      DCL d  ISDUtype ;
  
        START ;
              NEXTSTATE Connection ;

        STATE Connection ;
          INPUT ICONind ;
            conn:
              DECISION True /*draw (0.5)*/ ;
                (True):  OUTPUT ICONresp ;
                         NEXTSTATE Receiving ;
                ELSE:    OUTPUT IDISreq ;
                         NEXTSTATE Connection ;
              ENDDECISION ;
        ENDSTATE Connection ;

        STATE Receiving ;
          INPUT ICONind ;
              JOIN conn ;
          INPUT IDATind (d) ;
              DECISION True /*draw (0.5)*/ ;
                (True):  NEXTSTATE Receiving ;
                ELSE:    OUTPUT IDISreq ;
                         NEXTSTATE Connection ;
              ENDDECISION ;
        ENDSTATE Receiving ;

    ENDPROCESS User_Responder ;

  ENDBLOCK ENV ;


  BLOCK Ini_Station ;

    PROCESS Initiator REFERENCED /*%FILE init.sdl*/ ;

  ENDBLOCK Ini_Station ;


  BLOCK Resp_Station ;

    PROCESS Responder REFERENCED /*%FILE resp.sdl*/ ;

  ENDBLOCK Resp_Station ;


ENDSYSTEM Inres_Protocol ;

