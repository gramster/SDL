system ab_example; /* Simple Alternating-Bit example with loopback provider
								with no delay */

/* TESTS ENUMS */

	newtype Packet
		array (0) of character;
	endnewtype
	/* Signals that can be exchanged, and the types of their parameters */
	newtype Seq
		literals one, two;
	endnewtype

	signal
		u_data_req(Packet), u_data_rsp, u_data_ind(Packet),
		p_data_req(Seq,Packet), p_data_ind(Seq,Packet),
		p_ack_req(Seq), p_ack_ind(Seq);

	/* Forward declarations of blocks, needed for channel defns */

	block User referenced;
	block Protocol referenced;
	block Provider referenced;

	/* Channel definitions. Channels connect blocks. */

	channel upper_Ch
		from User to Protocol
			with u_data_req;
		from Protocol to User
			with u_data_ind, u_data_rsp;
	endchannel upper_Ch;

	channel lower_Ch
		from Protocol to Provider
			with p_data_req, p_ack_req;
		from Provider to Protocol
			with p_data_ind, p_ack_ind;
	endchannel lower_Ch;

	/* Provider Block */

	block Provider;
		process ProvControl referenced; /* Forward declaration with 
															 initial & max # of instances */
		connect lower_Ch and Comm;				/* External channel connection */

		/* Signalroutes connect processes within a block to each other
			and the external environment (channels) */

		signalroute Comm
			from ProvControl to env
				with p_data_ind, p_ack_ind;
			from env to ProvControl
				with p_data_req, p_ack_req;
	endblock Provider;

	/* Protocol block */

	block Protocol;
		process ProtControl referenced;
		connect lower_Ch and PComm;
		connect upper_Ch and UComm;
		signalroute PComm
			from ProtControl to env
				with p_data_req, p_ack_req;
			from env to ProtControl
				with p_data_ind, p_ack_ind;
		signalroute UComm
			from env to ProtControl
				with u_data_req;
			from ProtControl to env
				with u_data_ind, u_data_rsp;
	endblock Protocol;

	/* User block */

	block User;
		process UserControl referenced;
		signalroute Comm
			from UserControl to env
				with u_data_req;
			from env to UserControl
				with u_data_ind, u_data_rsp;
		connect upper_Ch and Comm;
	endblock User;
endsystem ab_example;

/* Each  block has a single controlling process */

process system ab_example/block Provider ProvControl(1,1);
	dcl sequ Seq;
	dcl data Packet;
	start;
		nextstate idle;
	state idle;
		input p_data_req(sequ,data);
			output p_data_ind(sequ,data);
			nextstate -;
		input p_ack_req(sequ);
			output p_ack_ind(sequ);
			nextstate -;
endprocess;

process system ab_example/block Protocol ProtControl(1,1);
	dcl sn Seq;
	dcl rn Seq;
	dcl sequ Seq;
	dcl data Packet;
	dcl tm Duration := 10.;
	timer Timeout;
	start;
		task sn := one;
		task rn := one;
		nextstate idle;
	state idle;
		input u_data_req(data);
			output p_data_req(sn, data);
			set (now + tm, Timeout); 
			nextstate wait;
		input p_ack_ind(seq);
			/* Do nothing */
			nextstate -;
	state wait;
		input p_ack_ind(sequ);
			decision sequ = sn;
			( false ) : /* do nothing */
				nextstate -;
			( true )  :
				decision sn = one;
				( true ) :
					task sn := two;
				( false) :
					task sn := one;
				enddecision;
				output u_data_rsp;
				nextstate idle;
			enddecision;
		input Timeout;
			output p_data_req(sn, data);
			set (now + tm, Timeout); 
			nextstate -;
	state *;
		input p_data_ind(sequ, data);
			decision sequ = rn;
			( false ) : /* do nothing */
			( true )  :
				output u_data_ind(data);
				output p_ack_req(rn);
				decision rn;
				( one ) :
					task rn := two;
				( two) :
					task rn := one;
				enddecision;
			enddecision;
			nextstate -;
endprocess;

process system ab_example/block User UserControl(1,1);
	dcl data Packet;
	dcl delayWait duration := 3.;
	timer sendDelay;
	start;
		nextstate sendfirst;
	state sendfirst;
		provided True;
			output u_data_req(data);
			nextstate idle;
	state idle;
		input u_data_rsp;
			set (now + delayWait, sendDelay);
			nextstate -;
		input u_data_ind(data);
			/* consume */
			nextstate -;
		input sendDelay;
			output u_data_req(data);
			nextstate -;
endprocess;


